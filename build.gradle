buildscript {
    // add Maven Central repository
    repositories {
        mavenCentral()
    }
    // add Cayenne Gradle Plugin
    dependencies {
        classpath group: 'org.apache.cayenne.plugins', name: 'cayenne-gradle-plugin', version: '4.1.B2'
    }
}

plugins {
    id 'java'

    // Apply the application plugin to add support for building an application
    id 'application'

    id 'idea'
}

apply plugin: 'org.apache.cayenne'

cayenne.defaultDataMap 'conf/datamap.map.xml'

repositories {

    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    // this is a shortcut for 'org.apache.cayenne:cayenne-server:VERSION_OF_PLUGIN'
    compile cayenne.dependency('server')

    compile 'org.postgresql:postgresql:42.2.5'
    compile 'org.mariadb.jdbc:mariadb-java-client:2.4.1'

    compile 'com.squareup:javapoet:1.11.1'
    compile 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.2'
    compile 'org.apache.commons:commons-lang3:3.9'
    compile 'commons-cli:commons-cli:1.4'
    compile 'org.reflections:reflections:0.9.11'
    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.6'


    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
    // https://mvnrepository.com/artifact/org.slf4j/slf4j-log4j12
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.26'
}

group = 'de.unidue'
version = '1.0-SNAPSHOT'
sourceCompatibility = '1.8'

sourceSets.main.java.srcDirs += file("src/gen/java")

// Define the main class for the application
mainClassName = 'de.unidue.dbmover.Cli'

task cli (type:JavaExec) {

    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath

    /* Can pass all the properties: */
    systemProperties System.getProperties()

    /*
    Need to split the space-delimited value in the exec.args

    Example:
    ./gradlew move -Dexec.args='-gen_pk_attributes src/main/resources/datamap.xml'
    */
    args System.getProperty("exec.args", "").split(" ")
}

cdbimport {
    // optional project file, will be created if missing
    cayenneProject 'conf/cayenne-source.xml'
    adapter 'org.apache.cayenne.dba.postgres.PostgresAdapter'

    def src = file('conf/cayenne-source.xml')
    def domain = new XmlParser().parse(src)
    def datasource = domain.node.'data-source'

    dataSource {
        driver datasource.driver.@value[0]
        url datasource.url.@value[0]
        username datasource.login.@userName[0]
        password datasource.login.@password[0]
    }

    def datamapFile = file('conf/datamap.map.xml')
    def datamap = new XmlSlurper().parse(datamapFile)
    def packageProperty = datamap.'*'.find { node ->
        node.name() == 'property' && node.@name == 'defaultPackage'
    }

    dbImport {
        defaultPackage packageProperty.@value[0].text()
        includeTable '.*'
        skipPrimaryKeyLoading true
        skipRelationshipsLoading true
        tableTypes 'TABLE'
        meaningfulPkTables '"*"'
    }
}

cgen {
    map 'conf/datamap.map.xml'
    destDir 'src/gen/java'
    makePairs true
}